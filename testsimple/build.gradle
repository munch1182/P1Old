def runAlone() {
    return rootProject.ext.component['testSimpleRunAlone']
}

if (runAlone()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

apply from: '../gradle/commonbuildktx.gradle'

android {
    buildFeatures {
        dataBinding = true
    }
    sourceSets {
        main {
            if (runAlone()) {
                manifest.srcFile 'src/main/component/AndroidManifest.xml'
                java.srcDirs = ['src/main/java', 'src/main/component/java']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude 'component/**'
                }
            }
        }
    }
}

kapt {
    correctErrorTypes true
    arguments {
        arg("room.schemaLocation", "$projectDir/schemas".toString())
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

dependencies {
    implementation project(path: ':common')
    kapt rootProject.ext.dependencies['arouter_compiler']

    api rootProject.ext.dependencies['paging']
    api rootProject.ext.dependencies['lifecycle_runtime_ktx']
    api rootProject.ext.dependencies['viewmodel_ktx']
    api rootProject.ext.dependencies['livedata_ktx']
    kapt rootProject.ext.dependencies['lifecycle_compiler_java8']

    api rootProject.ext.dependencies['hilt']
    api rootProject.ext.dependencies['hilt_viewmodel']
    kapt rootProject.ext.dependencies['hilt_viewmodel_compiler']
    kapt rootProject.ext.dependencies['hilt_compiler']

    api rootProject.ext.dependencies['room']
    api rootProject.ext.dependencies['room_ktx']
    kapt rootProject.ext.dependencies['room_compiler']

    api rootProject.ext.dependencies['work_ktx']
    debugImplementation rootProject.ext.dependencies['work_test']

    api rootProject.ext.dependencies['okhttp']
    api rootProject.ext.dependencies['okhttp_interceptor']

    api rootProject.ext.dependencies['retrofit']
    api rootProject.ext.dependencies['retrofit_gson']

    api rootProject.ext.dependencies['gson']

    api rootProject.ext.dependencies['glide']

    api rootProject.ext.dependencies['leakcanary']
}