import java.text.SimpleDateFormat

// 提取的公共build文件，用于统一版本和配置
// 使用此文件使用 apply from: '[路径/文件名]'
// 各个library或module可以覆盖配置
// 注意文件路径

//versions及dependencies定义在buildCom文件夹下version.gradle中
apply from: '../buildCom/gitversion.gradle'

static def getBuildTime() {
    new SimpleDateFormat("yyyyMMddHHmmss").format(new Date())
}

android {
    def buildTime = getBuildTime()
    def gitVersionCode = gitVersion['versionCode']
    def gitVersionName = gitVersion['versionName']

    compileSdk Version.compileSdk

    defaultConfig {
        minSdk Version.misSdk
        targetSdk Version.targetSdk

        versionCode gitVersionCode
        versionName gitVersionName


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def path = "key/key.properties"
    def keyProperties = findAndLoadKey(path)

    if (keyProperties != null) {
        signingConfigs {
            release {
                storeFile file(keyProperties['keyFile'])
                storePassword keyProperties['storePassword']
                keyAlias keyProperties['keyAlias']
                keyPassword keyProperties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            if (keyProperties != null) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            zipAlignEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'BUILD_TIME', "\"${buildTime}\""
            buildConfigField 'int', 'VERSION_CODE', "${gitVersionCode}"
            buildConfigField 'String', 'VERSION_NAME', "\"${gitVersionName}\""
        }
        debug {
            if (keyProperties != null) {
                signingConfig signingConfigs.release
            }
            debuggable true
            buildConfigField 'String', 'BUILD_TIME', "\"${buildTime}\""
            buildConfigField 'int', 'VERSION_CODE', "${gitVersionCode}"
            buildConfigField 'String', 'VERSION_NAME', "\"${gitVersionName}\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    /*module*/
    /*applicationVariants.all { variant ->
        setupModuleApk(variant, false, versionCode4Release)
    }*/
    /*alternatively*/
    /*lib*/
    /*libraryVariants.all { variant ->
        setupModuleApk(variant, true)
    }*/
}

private def findAndLoadKey(path) {
    def keyProperties = null
    try {
        def keystorePropertiesFile = rootProject.file(path)
        if (keystorePropertiesFile.exists()) {
            keyProperties = new Properties()
            keyProperties.load(new FileInputStream(keystorePropertiesFile))
        }
    } catch (Exception e) {
        e.printStackTrace()
    }
    keyProperties
}

static def setupModuleApk(variant, isLib, versionCode) {
    if (variant == null) {
        return
    }
    variant.outputs.each { output ->
        // 正式版下设置versionCode，参见gitVersion.gradle
        // 正式版更改输出app文件名，debug下不更改打包文件名，避免build生成的app文件越来越多
        if (!variant.getBuildType().isDebuggable()) {
            // 修改打包时versionCode的值
            // 此修改只对打包出来的apk有效，对直接运行的app无效
            // 且不会更改BuildConfig里的VERSION_CODE
            // 修改后的versionCode需要从PackageManager中获取
            output.versionCodeOverride = versionCode
            def date = new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+08"))
            // 文件名前缀 + 渠道名
            def startName = "app_release"
            def flavorName = variant.flavorName
            if (flavorName != null && flavorName != "") {
                startName += "_$flavorName"
            }
            // 文件名后缀
            def endName
            if (isLib) {
                endName = "_${variant.versionName}(${date}).aar"
            } else {
                endName = "_${variant.versionName}(${date}).apk"
            }
            output.outputFileName = startName + endName
        }
    }

}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation Deps.appcompat

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
